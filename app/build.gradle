apply plugin: 'com.android.application'

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd-HH-mm')
    return formattedDate
}
static def upFirstWorld(String name){

    name = name.substring(0, 1).toUpperCase() + name.substring(1)
    return name
}
android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.tuoanlan.jenkins_test"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {

        flavorDimensions "default"

        //开发环境并通过resValue 在不同的环境注入不同的值
        develop {
            dimension "default"
            buildConfigField "int", "ENV_TYPE", "1"
            applicationIdSuffix '.dev'
            resValue "string", "appName", "${rootProject.ext.appName}_Dev"

        }
        //准生产环境
        pre_product {
            dimension "default"
            buildConfigField "int", "ENV_TYPE", "2"
            applicationIdSuffix '.check'
            resValue "string", "appName", "${rootProject.ext.appName}_Check"

        }
        //生产环境,并通过resValue 在不同的环境注入不同的值
        product {
            dimension "default"
            buildConfigField "int", "ENV_TYPE", "3"
            resValue "string", "appName", rootProject.ext.appName

        }
    }
    //修改生成的apk名字及输出文件夹
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            //新名字
            def newName
            //时间戳
            def timeNow
            //输出文件夹
            def outDirectory
            //是否为Jenkins打包，输出路径不同
            if ("true".equals(IS_JENKINS)) {
                //Jenkins打包输出到服务器路径
                timeNow = BUILD_TIME
                //BUILD_PATH为服务器输出路径

                //JenkinsTest-v1.0.1-product-release-2019-10-18 10:35:34.apk
                newName = 'JenkinsTest-v' + APP_VERSION_NAME + '-' + upFirstWorld(variant.productFlavors[0].name) + '-' + upFirstWorld(variant.buildType.name) + '.apk'
            } else {
                //本机打包输出在本机路径
                timeNow = getDate()

                if ('debug'.equals(variant.buildType.name)) {
                    newName = "AutoBuildTest-v${APP_VERSION_NAME}-debug.apk"
                } else {
                    //AutoBuildTest-v1.0.1-xiaomi-release.apk
                    newName = 'JenkinsTest-v' + APP_VERSION_NAME + '-' + upFirstWorld(variant.productFlavors[0].name) + '-' + upFirstWorld(variant.buildType.name) + '.apk'
                }
            }
//            打包后放到 app/build/release 包下
            output.outputFileName = new File("../../../release/", newName)
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
